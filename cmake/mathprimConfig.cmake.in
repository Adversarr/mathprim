@PACKAGE_INIT@
include ( CMakeFindDependencyMacro )
find_dependency(Eigen3 REQUIRED)
# Fetch the dependency from the package
set(MATHPRIM_ENABLE_OPENMP @MATHPRIM_ENABLE_OPENMP@)
set(MATHPRIM_ENABLE_CUDA @MATHPRIM_ENABLE_CUDA@)
set(MATHPRIM_ENABLE_BLAS @MATHPRIM_ENABLE_BLAS@)
set(MATHPRIM_ENABLE_CHOLMOD @MATHPRIM_ENABLE_CHOLMOD@)
set(MATHPRIM_ENABLE_MKL @MATHPRIM_ENABLE_MKL@)

if (MATHPRIM_ENABLE_OPENMP)
  find_dependency(OpenMP REQUIRED)
endif()
if(MATHPRIM_ENABLE_CUDA)
  find_dependency(CUDAToolkit REQUIRED)
  message(STATUS "cudatk version: ${CUDAToolkit_VERSION}")
endif()
if(MATHPRIM_ENABLE_BLAS)
  if(MATHPRIM_ENABLE_MKL)
    find_dependency(MKL CONFIG REQUIRED)
    set(BLA_VENDOR "Intel" CACHE STRING "BLAS vendor")
    set(MATHPRIM_BLAS_VENDOR_INTEL_MKL ON)
  else()
    if(APPLE)
      # Prefer to use Accelerate framework on macOS
      set(BLA_VENDOR "Apple" CACHE STRING "BLAS vendor")
      set(MATHPRIM_BLAS_VENDOR_APPLE ON)
      set(ACCELERATE_NEW_LAPACK ON)
    else()
      set(BLA_VENDOR "OpenBLAS" CACHE STRING "BLAS vendor")
      set(MATHPRIM_BLAS_VENDOR_OPENBLAS ON)
    endif()

    find_dependency(BLAS REQUIRED)
  endif()
endif()
if(MATHPRIM_ENABLE_CHOLMOD)
  find_dependency(CHOLMOD REQUIRED)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/mathprimTargets.cmake")
